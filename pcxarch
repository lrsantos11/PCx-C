#! /bin/csh -f
#
#   pcxarch - Returns the machine's PCx environmental variable, PCx_ARCH.
#
# This file was copied from the PETSc package
#   http://www.mcs.anl.gov/petsc/petsc.html

if (-e /bin/uname) then
    set LARCH = `/bin/uname -s`
    if ("$LARCH" == "AIX") then
        set LARCH = "rs6000"
    else if ("$LARCH" == "HP-UX") then
	set LARCH = "hpux"
    else if ("$LARCH" == "Linux") then
	set LARCH = "linux"
    else if ("$LARCH" == "FreeBSD") then
	set LARCH = "freebsd"    
    else if ("$LARCH" != "IRIX" && "$LARCH" != "IRIX64") then 
        set LARCH = `/bin/uname -m`
    endif
else  if (-e /usr/bin/uname) then
    set LARCH = `/usr/bin/uname`
    if ("$LARCH" == "FreeBSD") then
      set LARCH = "freebsd"
    else
      echo "Unable to determine machines architecture"
      set LARCH = "unknown"
    endif
else
    echo "Unable to determine machines architecture"
    set LARCH = "unknown"
endif

set SunOSTest = `expr "$LARCH" : "\(....\)"`
if ("$SunOSTest" == "sun4") then
  set LARCH = sun4
  set Version = `/bin/uname -r`
  set MajorVersion = `expr "$Version" : "\(.\)"`
  if ("$MajorVersion" ==  5) then
    set LARCH = "solaris"
  endif
else if ("$LARCH" == "AIX") then
   set LARCH = "rs6000"
else if ("$LARCH" == "RIOS") then
   set LARCH = "rs6000"
else if ("$LARCH" == "sun4m") then
  set LARCH = "sun4"
else if ("$LARCH" == "iris4d") then
  set LARCH = "IRIX"
else if ("$LARCH" == "Linux" ) then 
  set LARCH = "linux"
else if ("$LARCH" == "CRAY Y-MP" ) then 
  set LARCH = "t3d"
endif
#
echo $LARCH
exit 0

